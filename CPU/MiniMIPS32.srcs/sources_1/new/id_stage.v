`include "defines.v"

module id_stage (
    input wire [`INST_ADDR_BUS] id_debug_wb_pc,  // 供调试使用的PC值，上板测试时务必删除该信号
    input wire                  cpu_rst_n,
    // 从取指阶段获得的PC值
    input wire [`INST_ADDR_BUS] id_pc_i,

    // 从指令存储器读出的指令字
    input wire [`INST_BUS] id_inst_i,

    // 从通用寄存器堆读出的数据 
    input wire [`REG_BUS] rd1,
    input wire [`REG_BUS] rd2,

    // 送至执行阶段的译码信息
    output wire [ `ALUTYPE_BUS] id_alutype_o,
    output wire [   `ALUOP_BUS] id_aluop_o,
    output wire [`REG_ADDR_BUS] id_wa_o,
    output wire                 id_wreg_o,
    //自己添加的信号
    output wire                 id_whilo_o,
    output wire                 id_mreg_o,
    output wire [     `REG_BUS] id_din_o,

    // 送至执行阶段的源操作数1、源操作数2
    output wire [`REG_BUS] id_src1_o,
    output wire [`REG_BUS] id_src2_o,


    // 送至读通用寄存器堆端口的使能和地址
    output wire                 rreg1,
    output wire [`REG_ADDR_BUS] ra1,
    output wire                 rreg2,
    output wire [`REG_ADDR_BUS] ra2,


    output wire id_whi_o,
    output wire id_wlo_o,
    
    //exe2id数据前推
    input  wire [`REG_ADDR_BUS]     exe2id_wa,
    input  wire                     exe2id_wreg,
    input  wire [`REG_BUS      ]    exe2id_wd,
    
    //mem2id数据前推
    input  wire [`REG_ADDR_BUS]     mem2id_wa,
    input  wire                     mem2id_wreg,
    input  wire [`REG_BUS      ]    mem2id_wd,  

    output [`INST_ADDR_BUS] debug_wb_pc  // 供调试使用的PC值，上板测试时务必删除该信号
    
    
);



  wire [`INST_BUS] id_inst = {id_inst_i[7:0], id_inst_i[15:8], id_inst_i[23:16], id_inst_i[31:24]};
  wire [      5:0                                                                                  ] op = id_inst[31:26];
  wire [      5:0                                                                                  ] func = id_inst[5 : 0];
  wire [      4:0                                                                                  ] rd = id_inst[15:11];
  wire [      4:0                                                                                  ] rs = id_inst[25:21];
  wire [      4:0                                                                                  ] rt = id_inst[20:16];
  wire [      4:0                                                                                  ] sa = id_inst[10:6];
  wire [     15:0                                                                                  ] imm = id_inst[15:0];
  assign debug_wb_pc = id_debug_wb_pc;




  /*-------------------- 第一级译码逻辑：确定当前需要译码的指令 --------------------*/
  wire inst_reg = ~|op;
  wire inst_add = inst_reg & func[5] & ~func[4] & ~func[3] & ~func[2] & ~func[1] & ~func[0];
  wire inst_subu = inst_reg & func[5] & ~func[4] & ~func[3] & ~func[2] & func[1] & func[0];
  wire inst_slt = inst_reg & func[5] & ~func[4] & func[3] & ~func[2] & func[1] & ~func[0];
  wire inst_and = inst_reg & func[5] & ~func[4] & ~func[3] & func[2] & ~func[1] & ~func[0];
  wire inst_mult = inst_reg & ~func[5] & func[4] & func[3] & ~func[2] & ~func[1] & ~func[0];
  wire inst_mfhi = inst_reg & ~func[5] & func[4] & ~func[3] & ~func[2] & ~func[1] & ~func[0];
  wire inst_mflo = inst_reg & ~func[5] & func[4] & ~func[3] & ~func[2] & func[1] & ~func[0];
  wire inst_sll = inst_reg & ~func[5] & ~func[4] & ~func[3] & ~func[2] & ~func[1] & ~func[0];
  wire inst_addu = inst_reg & func[5] & ~func[4] & ~func[3] & ~func[2] & ~func[1] & func[0];
  wire inst_sub = inst_reg & func[5] & ~func[4] & ~func[3] & ~func[2] & func[1] & ~func[0];
  wire inst_sltu = inst_reg & func[5] & ~func[4] & func[3] & ~func[2] & func[1] & func[0];
  wire inst_multu = inst_reg & ~func[5] & func[4] & func[3] & ~func[2] & ~func[1] & func[0];
  wire inst_nor = inst_reg & func[5] & ~func[4] & ~func[3] & func[2] & func[1] & func[0];
  wire inst_or = inst_reg & func[5] & ~func[4] & ~func[3] & func[2] & ~func[1] & func[0];
  wire inst_xor = inst_reg & func[5] & ~func[4] & ~func[3] & func[2] & func[1] & ~func[0];
  wire inst_sllv = inst_reg & ~func[5] & ~func[4] & ~func[3] & func[2] & ~func[1] & ~func[0];
  wire inst_sra = inst_reg & ~func[5] & ~func[4] & ~func[3] & ~func[2] & func[1] & func[0];
  wire inst_srl = inst_reg & ~func[5] & ~func[4] & ~func[3] & ~func[2] & func[1] & ~func[0];
  wire inst_srav = inst_reg & ~func[5] & ~func[4] & ~func[3] & func[2] & func[1] & func[0];
  wire inst_srlv = inst_reg & ~func[5] & ~func[4] & ~func[3] & func[2] & func[1] & ~func[0];
  wire inst_mthi = inst_reg & ~func[5] & func[4] & ~func[3] & ~func[2] & ~func[1] & func[0];
  wire inst_mtlo = inst_reg & ~func[5] & func[4] & ~func[3] & ~func[2] & func[1] & func[0];
  wire inst_jr = inst_reg & ~func[5] & ~func[4] & func[3] & ~func[2] & ~func[1] & ~func[0];
  wire inst_jalr = inst_reg & ~func[5] & ~func[4] & func[3] & ~func[2] & ~func[1] & func[0];
  wire inst_div = inst_reg & ~func[5] & func[4] & func[3] & ~func[2] & func[1] & ~func[0];
  wire inst_divu = inst_reg & ~func[5] & func[4] & func[3] & ~func[2] & func[1] & func[0];
  wire inst_break = inst_reg & ~func[5] & ~func[4] & func[3] & func[2] & ~func[1] & func[0];
  wire inst_syscall = inst_reg & ~func[5] & ~func[4] & func[3] & func[2] & ~func[1] & ~func[0];
  wire inst_ori = ~op[5] & ~op[4] & op[3] & op[2] & ~op[1] & op[0];
  wire inst_lui = ~op[5] & ~op[4] & op[3] & op[2] & op[1] & op[0];
  wire inst_addiu = ~op[5] & ~op[4] & op[3] & ~op[2] & ~op[1] & op[0];
  wire inst_sltiu = ~op[5] & ~op[4] & op[3] & ~op[2] & op[1] & op[0];
  wire inst_lb = op[5] & ~op[4] & ~op[3] & ~op[2] & ~op[1] & ~op[0];
  wire inst_lw = op[5] & ~op[4] & ~op[3] & ~op[2] & op[1] & op[0];
  wire inst_sb = op[5] & ~op[4] & op[3] & ~op[2] & ~op[1] & ~op[0];
  wire inst_sw = op[5] & ~op[4] & op[3] & ~op[2] & op[1] & op[0];
  wire inst_addi = ~op[5] & ~op[4] & op[3] & ~op[2] & ~op[1] & ~op[0];
  wire inst_slti = ~op[5] & ~op[4] & op[3] & ~op[2] & op[1] & ~op[0];
  wire inst_andi = ~op[5] & ~op[4] & op[3] & op[2] & ~op[1] & ~op[0];
  wire inst_xori = ~op[5] & ~op[4] & op[3] & op[2] & op[1] & ~op[0];
  wire inst_lbu = op[5] & ~op[4] & ~op[3] & op[2] & ~op[1] & ~op[0];
  wire inst_lh = op[5] & ~op[4] & ~op[3] & ~op[2] & ~op[1] & op[0];
  wire inst_lhu = op[5] & ~op[4] & ~op[3] & op[2] & ~op[1] & op[0];
  wire inst_sh = op[5] & ~op[4] & op[3] & ~op[2] & ~op[1] & op[0];
  wire inst_j = ~op[5] & ~op[4] & ~op[3] & ~op[2] & op[1] & ~op[0];
  wire inst_jal = ~op[5] & ~op[4] & ~op[3] & ~op[2] & op[1] & op[0];
  wire inst_beq = ~op[5] & ~op[4] & ~op[3] & op[2] & ~op[1] & ~op[0];
  wire inst_bne = ~op[5] & ~op[4] & ~op[3] & op[2] & ~op[1] & op[0];
  wire inst_bgez = ~op[5] & ~op[4] & ~op[3] & ~op[2] & ~op[1] & op[0] & ~rt[4] & ~rt[3] & ~rt[2] & ~rt[1] & rt[0];
  wire inst_bgtz = ~op[5] & ~op[4] & ~op[3] & op[2] & op[1] & op[0] & ~rt[4] & ~rt[3] & ~rt[2] & ~rt[1] & ~rt[0];
  wire inst_blez = ~op[5] & ~op[4] & ~op[3] & op[2] & op[1] & ~op[0] & ~rt[4] & ~rt[3] & ~rt[2] & ~rt[1] & ~rt[0];
  wire inst_bltz = ~op[5] & ~op[4] & ~op[3] & ~op[2] & ~op[1] & op[0] & ~rt[4] & ~rt[3] & ~rt[2] & ~rt[1] & ~rt[0];
  wire inst_bgezal = ~op[5] & ~op[4] & ~op[3] & ~op[2] & ~op[1] & op[0] & rt[4] & ~rt[3] & ~rt[2] & ~rt[1] & rt[0];
  wire inst_bltzal = ~op[5] & ~op[4] & ~op[3] & ~op[2] & ~op[1] & op[0] & rt[4] & ~rt[3] & ~rt[2] & ~rt[1] & ~rt[0];
  wire inst_eret = ~op[5] & op[4] & ~op[3] & ~op[2] & ~op[1] & ~op[0] & ~func[5] & func[4] & func[3] & ~func[2] & ~func[1] & ~func[0];
  wire inst_mfc0 = ~op[5] & op[4] & ~op[3] & ~op[2] & ~op[1] & ~op[0] & ~id_inst[23];
  wire inst_mtc0 = ~op[5] & op[4] & ~op[3] & ~op[2] & ~op[1] & ~op[0] & id_inst[23];

  /*------------------------------------------------------------------------------*/

  /*-------------------- 第二级译码逻辑：生成具体控制信号 --------------------*/
  // 操作类型alutype
  assign id_alutype_o[2] = (inst_sll | inst_sllv | inst_srl | inst_srlv | inst_sra | inst_srav);
  assign id_alutype_o[1] = (inst_and | inst_mfhi | inst_mflo | inst_ori | inst_lui | inst_andi | inst_xori | inst_or | inst_xor | inst_nor | inst_mtlo | inst_mthi);
  assign id_alutype_o[0] = (inst_mfhi | inst_mflo | inst_lb | inst_lw | inst_sb | inst_sh | inst_sw | inst_add | inst_subu | inst_slt | inst_addiu | inst_sltiu | inst_addi | inst_slti | inst_addu | inst_sub | inst_sltu | inst_mtlo | inst_mthi | inst_lbu | inst_lh | inst_lhu);

  // 内部操作码aluop
  assign id_aluop_o[7] = (inst_lb | inst_lw | inst_sb | inst_sw | inst_lbu | inst_lh | inst_lhu | inst_sh | inst_syscall | inst_eret | inst_break | inst_mfc0 | inst_mtc0);
  assign id_aluop_o[6] = (inst_bgez | inst_bgtz | inst_blez | inst_bltz | inst_bltzal | inst_bgezal | inst_jalr | inst_div | inst_divu);
  assign id_aluop_o[5] = (inst_slt | inst_sltiu | inst_slti | inst_sltu | inst_or | inst_xor | inst_xori | inst_sra | inst_srl | inst_srav | inst_srlv | inst_j | inst_jr | inst_jal | inst_beq | inst_bne);
  assign id_aluop_o[4]   = (inst_and | inst_add | inst_subu | inst_mult | inst_sll |
                                                                  inst_addiu | inst_ori | inst_lb | inst_lw | inst_sb | inst_sw |
                                                                  inst_addi | inst_addu | inst_sub | inst_multu | inst_andi |
                                                                  inst_nor | inst_sllv | inst_lbu | inst_lh | inst_lhu | inst_sh |
                                                                  inst_j | inst_jr | inst_jal | inst_beq | inst_bne | inst_div | inst_divu);
  assign id_aluop_o[3]   = (inst_and | inst_add | inst_subu | inst_mfhi | inst_mflo |
                                                                  inst_addiu | inst_ori | inst_sb | inst_sw | inst_addi | inst_slti |
                                                                  inst_sltu | inst_andi | inst_nor | inst_sra | inst_srl | inst_srav | inst_srlv |
                                                                  inst_mthi | inst_mtlo | inst_sh | inst_bne | inst_bltzal | inst_bgezal | inst_jalr |
                                                                  inst_break | inst_mfc0 | inst_mtc0);
  assign id_aluop_o[2]   = (inst_and | inst_slt | inst_mult | inst_mfhi | inst_mflo |
                                                                  inst_sltiu | inst_ori | inst_lui | inst_addu | inst_sub | inst_multu |
                                                                  inst_andi | inst_nor | inst_srl | inst_srlv | inst_mthi | inst_mtlo |
                                                                  inst_lh | inst_lhu | inst_beq | inst_bltz | inst_syscall | inst_eret |
                                                                  inst_mfc0 | inst_mtc0);
  assign id_aluop_o[1] = (inst_subu | inst_slt | inst_sltiu | inst_lw | inst_sw | inst_addi | inst_addu | inst_sub | inst_andi | inst_nor | inst_xori | inst_sllv | inst_sra | inst_srav | inst_mthi | inst_mtlo | inst_lhu | inst_jal | inst_blez | inst_jalr | inst_syscall | inst_eret);
  assign id_aluop_o[0] = (inst_subu | inst_mflo | inst_sll | inst_addiu | inst_sltiu | inst_ori | inst_lui | inst_addu | inst_sltu | inst_multu | inst_nor | inst_xor | inst_srav | inst_srlv | inst_mtlo | inst_lbu | inst_sh | inst_jr | inst_bgtz | inst_bgezal | inst_divu | inst_eret | inst_mtc0);


  // 写通用寄存器使能信号
  assign id_wreg_o       = (inst_add | inst_subu | inst_slt | inst_and | inst_mfhi | inst_mflo | inst_sll |
                              inst_ori | inst_addiu | inst_lui | inst_sltiu | inst_lb | inst_lw | inst_addi |
                              inst_addu | inst_sub | inst_slti | inst_sltu | inst_andi | inst_nor | inst_or |
                              inst_xor | inst_xori | inst_sllv | inst_sra | inst_srl | inst_srav | inst_srlv |
                              inst_lbu | inst_lh | inst_lhu | inst_jal | inst_jalr | inst_bgezal | inst_bltzal |
                              inst_mfc0);

  // 读通用寄存器堆端口1使能信号
  assign rreg1 = (inst_add | inst_subu | inst_slt | inst_and | inst_mult |
                              inst_addiu | inst_ori | inst_sltiu | inst_lb | inst_lw | inst_sb | inst_sw |
                              inst_addi | inst_addu | inst_sub | inst_slti | inst_sltu | inst_multu |
                              inst_andi | inst_nor | inst_or | inst_xor | inst_xori | inst_sllv |
                              inst_srav | inst_srlv | inst_mthi | inst_mtlo | inst_lbu | inst_lh | inst_lhu | 
                              inst_sh | inst_jr | inst_beq | inst_bne | inst_bgez | inst_bgtz | inst_blez | 
                              inst_bltz | inst_bltzal | inst_bgezal | inst_jalr | inst_div | inst_divu);

  // 读通用寄存器堆读端口2使能信号
  assign rreg2 = (cpu_rst_n == `RST_ENABLE) ? 1'b0 : 
                             (inst_add | inst_subu | inst_slt | inst_and | inst_mult | inst_sll | inst_sb | 
                              inst_sw | inst_addu | inst_sub | inst_sltu | inst_multu | inst_nor | inst_or |
                              inst_xor | inst_sllv | inst_sra | inst_srl | inst_srav | inst_srlv | inst_sh |
                              inst_beq | inst_bne | inst_div | inst_divu | inst_mtc0);

  //  l指令
  assign id_mreg_o = (inst_lb | inst_lw | inst_lbu | inst_lh | inst_lhu);
  assign id_whilo_o = (inst_mult | inst_multu | inst_div | inst_divu);

  wire shift = inst_sll | inst_sra | inst_srl;

  wire immsel = inst_ori | inst_lui | inst_lw | inst_lb | inst_sb | inst_sh | inst_sw | inst_addiu | inst_sltiu | inst_addi | inst_slti | inst_andi | inst_xori | inst_lbu | inst_lh | inst_lhu;

  wire rtsel = inst_ori | inst_lui | inst_lb | inst_lw | inst_addiu | inst_sltiu | inst_addi | inst_slti | inst_andi | inst_xori | inst_lbu | inst_lh | inst_lhu;

  wire sext = inst_lb | inst_lw | inst_sb | inst_sh | inst_sw | inst_addiu | inst_sltiu | inst_addi | inst_slti | inst_lbu | inst_lh | inst_lhu;

  wire upper = inst_lui;


  /*------------------------------------------------------------------------------*/

  // 读通用寄存器堆端口1的地址为rs字段，读端口2的地址为rt字段
  assign ra1      = rs;
  assign ra2      = rt;
  assign id_whi_o = inst_mthi;
  assign id_wlo_o = inst_mtlo;

  wire [31:0] imm_ext = (upper == `UPPER_ENABLE) ? (imm << 16) : (sext == `SIGNED_EXT) ? {{16{imm[15]}}, imm} : {{16{1'b0}}, imm};

  // 获得待写入目的寄存器的地址（rt或rd）
  assign id_wa_o   = (rtsel == `RT_ENABLE) ? rt : rd;

  
  //前推信号
    reg [1:0] fwrd1;
    reg [1:0] fwrd2;
    reg [`REG_BUS] din;
    always @(*) begin
        if(exe2id_wreg == `WRITE_ENABLE && exe2id_wa == rs)begin
            fwrd1 = 2'b01;
        end
        else if(mem2id_wreg == `WRITE_ENABLE && mem2id_wa == rs)begin
            fwrd1 = 2'b10;
        end
        else begin
            fwrd1 = 2'b00;
        end
        if(exe2id_wreg == `WRITE_ENABLE && exe2id_wa == rt)begin
            fwrd2 = 2'b01;
        end
        else if(mem2id_wreg == `WRITE_ENABLE && mem2id_wa == rt)begin
            fwrd2 = 2'b10;
        end
        else begin
            fwrd2 = 2'b00;
        end
    end
    reg [`REG_BUS      ] src1;
    reg [`REG_BUS      ] src2;
    always @(*) begin
        if(fwrd2 == 2'b00)begin
            din = rd2;
        end
        else if(fwrd2 == 2'b01)begin
            din = exe2id_wd;
        end
        else if(fwrd2 == 2'b10)begin
            din = mem2id_wd;
        end
        if(shift == `SHIFT_ENABLE)begin
            src1 = sa;
        end
        else if(fwrd1 == 2'b00)begin
            src1 = rd1;
        end
        else if(fwrd1 == 2'b01)begin
            src1 = exe2id_wd;
        end
        else if(fwrd1 == 2'b10)begin
            src1 = mem2id_wd;
        end
        if(immsel == `IMM_ENABLE)begin
            src2 = imm_ext;
        end
        else if(fwrd2 == 2'b00)begin
            src2 = rd2;
        end
        else if(fwrd2 == 2'b01)begin
            src2 = exe2id_wd;
        end
        else if(fwrd2 == 2'b10)begin
            src2 = mem2id_wd;
        end
    end
  // 获得源操作数1
  assign id_src1_o = src1;

  // 获得源操作数2
    assign id_src2_o = src2 ;           
    assign id_din_o = din;
endmodule
